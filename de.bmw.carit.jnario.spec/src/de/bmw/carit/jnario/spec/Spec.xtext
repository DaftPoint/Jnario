grammar de.bmw.carit.jnario.spec.Spec with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/Xtend2" as xtend2

generate spec "http://www.bmw.de/carit/jnario/spec/Spec"

SpecFile:
('package' packageName=QualifiedName)? 
	imports+=Import*
	elements+=ExampleGroup*
;

Import returns xtend2::XtendImport: 
	'import' (
		(static?='static' extension?='extension'? importedType=[types::JvmType|QualifiedName] '.' '*') | 
		importedType=[types::JvmType|QualifiedName] | 
		importedNamespace=QualifiedNameWithWildCard
	)
;

QualifiedNameWithWildCard :
	QualifiedName  '.' '*';

ExampleGroup: preamble=('describe'|'context') target=[types::JvmIdentifiableElement|ValidID]? name=STRING? '{'
       elements+=AbstractElement*
'}'
;

AbstractElement:
	Example | Member | ExampleGroup
;

Member:
	type=JvmTypeReference name=ValidID ('=' right=XExpression)?
;

Example:
	preamble=('specify'|'it') feature=[types::JvmIdentifiableElement|ValidID]? name=STRING? 
		body=XBlockExpression?
;

terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'";

