grammar de.bmw.carit.jnario.spec.Spec with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate spec "http://www.bmw.de/carit/jnario/spec/Spec"

SpecFile:
('package' packageName=QualifiedName)? 
	imports+=Import*
	elements+=ExampleGroup*
;

Import:
    'import' importedNamespace=QualifiedNameWithWildCard;

QualifiedNameWithWildCard :
    QualifiedName  ('.' '*')?;

ExampleGroup: preamble=('describe'|'context') target=[types::JvmIdentifiableElement|ValidID]? name=STRING? '{'
       elements+=AbstractElement*
'}'
;

AbstractElement:
	Example | Member | ExampleGroup
;

Member:
	type=JvmTypeReference name=ValidID ('=' right=XExpression)?
;

Example:
	preamble=('specify'|'it') feature=[types::JvmIdentifiableElement|ValidID]? name=STRING? 
		body=XBlockExpression?
;

