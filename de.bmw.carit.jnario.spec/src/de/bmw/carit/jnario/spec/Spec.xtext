grammar de.bmw.carit.jnario.spec.Spec with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

generate spec "http://www.bmw.de/carit/jnario/spec/Spec"

Spec:
'package' name=QualifiedName 
	imports+=Import*
	elements+=ExampleGroup*
;

Import:
    'import' importedNamespace=QualifiedNameWithWildCard;

QualifiedNameWithWildCard :
    QualifiedName  ('.' '*')?;


ExampleGroup: 'describes' {ExampleGroup} target=JvmTypeReference? name=STRING? '{'
       elements+=AbstractElement*
'}'
;

AbstractElement:
	Context | Example | Member
;

Member:
	type=JvmTypeReference name=ValidID ('=' right=XExpression)?
;


Context:
	'context' name=STRING '{'
		elements+=AbstractElement*
	'}'
;

Example:
	'it' feature=[types::JvmIdentifiableElement|ValidID]? name=STRING? 
		body=XBlockExpression
;

