grammar de.bmw.carit.jnario.Jnario with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations
generate jnario "http://www.bmw.de/carit/jnario/Jnario"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

Jnario:
	('package' package=QualifiedName)?
	(imports += Import)*
	name=FEATURE_TEXT
	background = Background?
	scenarios+=Scenario+
;

Import : 
	'import' (static?='static' extension?='extension'?)? importedNamespace=QualifiedNameWithWildCard
;

QualifiedNameWithWildCard :
	QualifiedName  ('.' '*')?
;

Background:
	BACKGROUND_TEXT
	steps+=Given|And
;

Scenario:
	name=SCENARIO_TEXT
	steps+=Step*
	examples+=Examples*
; 

Step:
	Given|When|Then|And
;

Given:
	name=GIVEN_TEXT code=Code
;

When:
	name=WHEN_TEXT code=Code
;

Then:
	name=THEN_TEXT code=Code
;

And:
	name=AND_TEXT code=Code
;

Code:
	{Code}
	annotations+=XAnnotation* 
	blockExpression=BlockExpression?
;

BlockExpression returns xbase::XBlockExpression:
	expressions+= XExpressionInsideBlock+
;

Examples:
	name=EXAMPLE_TEXT
	heading = ExampleHeading
	(rows += ExampleRow)*
;

ExampleHeading:
	parts+=ExampleHeadingCell+ EXAMPLE_ROW_END
;

ExampleHeadingCell returns xbase::XVariableDeclaration:
	{ExampleHeadingCell} '|' name=ValidID
;

ExampleRow:
	parts+=ExampleCell+ EXAMPLE_ROW_END
;

ExampleCell:
	'|'name=XExpression
;

terminal FEATURE_TEXT: 'Feature' COLON MNL;
terminal BACKGROUND_TEXT: 'Background' COLON MNL;
terminal SCENARIO_TEXT: 'Scenario' COLON MNL;
terminal GIVEN_TEXT: 'Given ' MNL;
terminal WHEN_TEXT: 'When ' MNL;
terminal THEN_TEXT: 'Then ' MNL;
terminal AND_TEXT: 'And ' MNL;
terminal EXAMPLE_TEXT: 'Examples' COLON MNL;
terminal EXAMPLE_ROW_END: '|' SPACES NL;

terminal fragment TEXT_MULTI_LINE: !('\r'|'\n')* '\\' NL;
terminal fragment TEXT_AND_NL: !('\r'|'\n'|'\\')* NL;
terminal fragment MNL: (TEXT_MULTI_LINE)* TEXT_AND_NL;
terminal fragment COLON: (' '|'\t')* ':';
terminal fragment NL: '\r'? '\n'?;
terminal fragment SPACES: (' '|'\t')*;