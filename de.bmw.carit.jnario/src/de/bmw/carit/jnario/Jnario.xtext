grammar de.bmw.carit.jnario.Jnario with org.eclipse.xtext.xbase.Xbase 
generate jnario "http://www.bmw.de/carit/jnario/Jnario"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

Jnario:
	name=FEATURE_TEXT
	scenarios+=Scenario+
;

Scenario:
	name=SCENARIO_TEXT
	given = Given
	when = When
	then = Then
; 

Step:
	Given|When|Then
;

Given:
	name+=GIVEN_TEXT code+=Code?
	(name+=AND_TEXT code+=Code?)*
;

When:
	name+=WHEN_TEXT code+=Code?
	(name+=AND_TEXT code+=Code?)*
;

Then:
	name+=THEN_TEXT code+=Code?
	(name+=AND_TEXT code+=Code?)*
;

Code returns xbase::XBlockExpression:
	expressions+= XExpressionInsideBlock+
;

terminal FEATURE_TEXT: 'Feature' (' '|'\t')* ':' (MULTI_LINE)* NEW_LINE;
terminal SCENARIO_TEXT: 'Scenario: ' (MULTI_LINE)* NEW_LINE;
terminal GIVEN_TEXT: 'Given ' (MULTI_LINE)* NEW_LINE;
terminal WHEN_TEXT: 'When ' (MULTI_LINE)* NEW_LINE;
terminal THEN_TEXT: 'Then ' (MULTI_LINE)* NEW_LINE;
terminal AND_TEXT: 'And ' (MULTI_LINE)* NEW_LINE;

terminal fragment MULTI_LINE: !('\r'|'\n')* '\\' '\r'? '\n' ;
terminal fragment NEW_LINE: !('\r'|'\n')*  '\r'? '\n';