/*******************************************************************************
 * Copyright (c) 2012 BMW Car IT and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/

grammar de.bmw.carit.jnario.Jnario with org.eclipse.xtext.xtend2.Xtend2
generate jnario "http://www.bmw.de/carit/jnario/Jnario"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/Xtend2" as xtend
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

Jnario returns xtend::XtendFile:
	{Jnario}
	('package' package=QualifiedName)?
	imports += Import*
	(xtendClass = Feature)?
;

Feature returns xtend::XtendClass:
	{Feature}
	annotations+=XAnnotation*
	name=FEATURE_TEXT
	description=DESCRIPTION?
	background = Background?
	members+=Scenario*;
	
DESCRIPTION:
	IN_ORDER_TEXT
	AS_A_TEXT
	I_WANT_TEXT
;

Background:
	{Background}
	BACKGROUND_TEXT
	members+=Member*
	steps+=GivenSteps*;

Scenario returns xtend::XtendMember:
	{Scenario}
	name=SCENARIO_TEXT
	members+=Member*
	steps+=GivenSteps*
	steps+=(When|WhenRef)?
	steps+=(Then|ThenRef)*
	examples+=ExampleTable*;

Step returns xtend::XtendMember:
	Given|When|Then|And|GivenRef|WhenRef|ThenRef|AndRef;
	
GivenSteps returns xtend::XtendMember:
	Given|GivenRef
;
	
GivenRef returns xtend::XtendMember:
	{GivenRef}
	reference=[Given|GIVEN_TEXT]
	and+=(And|AndRef)*
;

Given returns xtend::XtendMember:
	{Given}
	name=GIVEN_TEXT code=Code
	and+=(And|AndRef)*
;
	
WhenRef returns xtend::XtendMember:
	{WhenRef}
	reference=[When|WHEN_TEXT]
	and+=(And|AndRef)*
;

When returns xtend::XtendMember:
	{When}
	name=WHEN_TEXT code=Code
	and+=(And|AndRef)*
;

ThenRef returns xtend::XtendMember:
	{ThenRef}
	reference=[Then|THEN_TEXT]
	and+=(And|AndRef)*
;

Then returns xtend::XtendMember:
	{Then}
	name=THEN_TEXT code=Code
	and+=(And|AndRef)*;
	
AndRef returns xtend::XtendMember:
	{AndRef}
	reference=[And|AND_TEXT]
;
	
And returns xtend::XtendMember:
	{And}
	name=AND_TEXT code=Code
;

Code:
	annotations+=XAnnotation* 
	blockExpression=BlockExpression;

BlockExpression returns xbase::XBlockExpression:
	(expressions+=XExpressionInsideBlock ';'?)+;

ExampleTable:
	name=EXAMPLE_TEXT
	heading = ExampleHeading
	(rows += ExampleRow)*;

ExampleHeading:
	parts+=ExampleHeadingCell+ EXAMPLE_ROW_END;
 
ExampleHeadingCell returns xtend::XtendField:
	(PIPE | PIPE_SPACES) name=ValidID ;

ExampleRow:
	parts+=ExampleCell+ EXAMPLE_ROW_END;

ExampleCell:
	(PIPE | PIPE_SPACES) name=XExpression;


terminal FEATURE_TEXT: 'Feature' COLON MNL;
terminal BACKGROUND_TEXT: 'Background' COLON MNL;
terminal SCENARIO_TEXT: 'Scenario' COLON MNL;
terminal GIVEN_TEXT: 'Given ' MNL;
terminal WHEN_TEXT: 'When ' MNL;
terminal THEN_TEXT: 'Then ' MNL;
terminal AND_TEXT: 'And ' MNL;
terminal EXAMPLE_TEXT: 'Examples' COLON MNL;

terminal IN_ORDER_TEXT: 'In order to' TEXT_AND_NL;
terminal AS_A_TEXT: 'As a' TEXT_AND_NL;
terminal I_WANT_TEXT: 'I want' TEXT_AND_NL;


terminal PIPE : '|';
terminal PIPE_SPACES: PIPE (' '|'\t')+;
terminal EXAMPLE_ROW_END: (PIPE | PIPE_SPACES) '\r'? '\n';


terminal fragment TEXT_MULTI_LINE: !('\r'|'\n')* '\\' NL;
terminal fragment TEXT_AND_NL: !('\r'|'\n'|'\\')* NL;
terminal fragment MNL: (TEXT_MULTI_LINE)* TEXT_AND_NL;
terminal fragment COLON: SPACES ':';
terminal fragment NL: '\r'? '\n'?;
terminal fragment SPACES: (' '|'\t')*;